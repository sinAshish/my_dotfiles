# vim:et sts=2 sw=2 ft=zsh
#
# Unarchives files
#
builtin emulate -L zsh -o ERR_RETURN

if (( # < 1 )); then
  print -u2 "usage: ${0} <archive_name.ext>..."
  return 2
fi

# using pbunzip2/unpigz provides little to decompression time; the benefit is mainly in compression time.

while (( # > 0 )); do
  case ${1} in
    (*.7z|*.001) 7za x ${1} ;;
    (*.rar) (( ${+commands[unrar]} )) && unrar x -ad ${1} || rar x -ad ${1} ;;
    (*.tar.bz|*.tar.bz2|*.tbz|*.tbz2) tar -xvjf ${1} ;;
    (*.tar.gz|*.tgz) tar -xvzf ${1} ;;
    (*.tar.lzma|*.tlz) tar --lzma --help &>/dev/null && XZ_OPT=-T0 tar --lzma -xvf ${1} \
      || lzcat ${1} | tar -xvf - ;;
    (*.tar.xz|*.txz) tar -J --help &>/dev/null && XZ_OPT=-T0 tar -xvJf ${1} \
      || xzcat ${1} | tar -xvf - ;;
    (*.tar.zst|*.tzst) XZ_OPT=-T0 tar --use-compress-program=unzstd -xvf ${1} ;;
    (*.tar) tar -xvf ${1} ;;
    (*.zip) unzip ${1};;
    (*.bz|*.bz2) bunzip2 ${1} ;;
    (*.gz) gunzip ${1} ;;
    (*.lzma) unlzma -T0 ${1} ;;
    (*.xz) unxz -T0 ${1} ;;
    (*.zst) zstd -T0 -d ${1} ;;
    (*.Z) uncompress ${1} ;;
    (*) print -u2 "${0}: unknown archive type: ${1}" ;;
  esac
  shift
done
